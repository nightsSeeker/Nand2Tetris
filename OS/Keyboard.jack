class Keyboard {
    static Array keyboard;
    
    // Initializes the keyboard
    function void init() {
        let keyboard = 24576;
        return;
    }
    
    // Returns the ASCII code of the currently pressed key
    function char keyPressed() {
        return keyboard[0];
    }
    
    // Reads the next character from the keyboard
    function char readChar() {
        var char c;
        while (Keyboard.keyPressed() = 0) {}
        let c = Keyboard.keyPressed();
        while (~(Keyboard.keyPressed() = 0)) {}
        do Output.printChar(c);
        return c;
    }
    
    // Prints the message on the screen and reads the next line
    function String readLine(String message) {
        var String line;
        var char c;
        
        do Output.printString(message);
        let line = String.new(50);
        
        while (true) {
            let c = Keyboard.readChar();
            if (c = String.newLine()) {
                do Output.println();
                return line;
            }
            if (c = String.backSpace()) {
                do line.eraseLastChar();
            }
            else {
                do line.appendChar(c);
            }
        }
        return line;
    }
    
    // Prints the message on the screen and reads the next integer
    function int readInt(String message) {
        var String line;
        let line = Keyboard.readLine(message);
        return line.intValue();
    }
}